plugins {
    id 'java'
    id "org.openapi.generator" version "5.0.1"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

ext {
    swagger_annotations_version = "1.5.24"
    jackson_version = "2.10.3"
    jackson_databind_version = "2.10.3"
    jackson_databind_nullable_version = "0.2.1"
    jackson_threetenbp_version = "2.9.10"
    feign_version = "10.11"
    feign_form_version = "3.8.0"
    scribejava_version = "8.0.0"
}

dependencies {
    implementation "io.swagger:swagger-annotations:$swagger_annotations_version"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "io.github.openfeign:feign-core:$feign_version"
    implementation "io.github.openfeign:feign-jackson:$feign_version"
    implementation "io.github.openfeign:feign-slf4j:$feign_version"
    implementation "io.github.openfeign:feign-okhttp:$feign_version"
    implementation "io.github.openfeign.form:feign-form:$feign_form_version"
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_databind_version"
    implementation "org.openapitools:jackson-databind-nullable:$jackson_databind_nullable_version"
    implementation "com.github.joschi.jackson:jackson-datatype-threetenbp:$jackson_threetenbp_version"
    implementation "com.brsanthu:migbase64:2.2"
    implementation "com.github.scribejava:scribejava-core:$scribejava_version"
    implementation "com.brsanthu:migbase64:2.2"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.1'
    implementation 'com.auth0:java-jwt:3.13.0'

}

openApiValidate {
    inputSpec = "$project.parent.projectDir/apis/ref-api-1.3.0.yml".toString()
}

openApiGenerate {
    generatorName = "java"
    skipValidateSpec = true
    inputSpec = "$project.parent.projectDir/apis/ref-api-1.3.0.yml".toString()
    outputDir = "$project.buildDir/generated".toString()
    apiPackage = "fr.pcscol.codegen.feign.ref.api"
    invokerPackage = "fr.pcscol.codegen.feign.ref.invoker"
    modelPackage = "fr.pcscol.codegen.feign.ref.model"
    configOptions = [
            dateLibrary: "java8"
    ]
    library = "feign"
}

compileJava.dependsOn tasks.openApiGenerate

jar {
    manifest {
        attributes(
                'Main-Class': 'fr.pcscol.codegen.feign.JavaFeignClientApiInvocation'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

sourceSets {
    main {
        java {
            srcDir "$project.buildDir/generated/src/main/java"
        }
    }
}
