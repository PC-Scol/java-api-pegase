plugins {
    id 'java'
    id "org.openapi.generator" version "5.0.1"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

ext {
    swagger_annotations_version = "1.5.22"
    jackson_version = "2.10.4"
    ws_rs_version = "2.1.1"
}

dependencies {
    // Compilation des classes java générées par openApiGenerate
    implementation "io.swagger:swagger-annotations:$swagger_annotations_version"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    implementation "org.openapitools:jackson-databind-nullable:0.2.1"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation "javax.ws.rs:javax.ws.rs-api:$ws_rs_version"
}

openApiValidate {
    inputSpec = "$project.projectDir/src/main/resources/ref-api-1.3.0.yml".toString()
}

openApiGenerate {
    generatorName = "java"
    skipValidateSpec = true
    inputSpec = "$project.projectDir/src/main/resources/ref-api-1.3.0.yml".toString()
    outputDir = "$project.buildDir/generated".toString()
    apiPackage = "fr.pcscol.codegen.nativ.ref.api"
    invokerPackage = "fr.pcscol.codegen.nativ.ref.invoker"
    modelPackage = "fr.pcscol.codegen.nativ.ref.model"
    configOptions = [
            dateLibrary: "java8"
    ]
    library = "native"
}

compileJava.dependsOn tasks.openApiGenerate

jar {
    manifest {
        attributes(
                'Main-Class': 'fr.pcscol.codegen.nativ.JavaNativeClientApiInvocation'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

sourceSets {
    main {
        java {
            srcDir "$project.buildDir/generated/src/main/java"
        }
    }
}
